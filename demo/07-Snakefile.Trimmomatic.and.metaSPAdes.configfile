# Using --configfile and the config[] variable
# show -np Snakemake option

import os
cwd = os.getcwd()
workdir: 'output'

# Retrieving the content of the 'samples' config variable passed to Snakemake using --config samples=x,y,z
SAMPLES = config['samples'].split(',')

rule run_all:
  input: 
    expand('{sample}/metaspades_output/contigs.fasta', sample=SAMPLES)

rule run_trimmomatic:
  input:
    r1='{}/data/{{sample}}_r1.fastq.gz'.format(cwd),
    r2='{}/data/{{sample}}_r2.fastq.gz'.format(cwd)
  output:
    '{sample}/output_1P.fastq.gz',
    '{sample}/output_2P.fastq.gz'
  shell:
    '''
    TRIMMOMATIC_ADAPTER_PATH=$CONDA_PREFIX/share/trimmomatic-0.38-1/adapters/TruSeq3-PE.fa 
    trimmomatic PE -threads 4 -phred33 \
      -trimlog {wildcards.sample}/trim.log \
      {input.r1} {input.r2} \
      -baseout {wildcards.sample}/output.fastq.gz \
      ILLUMINACLIP:$TRIMMOMATIC_ADAPTER_PATH:2:30:10:1:true \
      LEADING:5 TRAILING:5 MINLEN:50 HEADCROP:26
    '''

rule run_metaspades:
  input:
    r1='{sample}/output_1P.fastq.gz',
    r2='{sample}/output_2P.fastq.gz'
  output:
    '{sample}/metaspades_output/contigs.fasta'
  shell:
    '''
    metaspades.py -1 {input[0]} -2 {input[1]} -o {wildcards.sample}/metaspades_output --tmp-dir {wildcards.sample}/metaspades_output/tmp -t 4
    '''

